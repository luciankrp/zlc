managed; // implementation in class zbp_r_lc_travel_m unique;
strict ( 2 );
with draft;

define behavior for ZR_LC_TRAVEL_M alias Travel
implementation in class zbp_r_lc_travel_m unique
persistent table zlc_travel_m
draft table zlc_travel_d
lock master
total etag LastChgAt
authorization master ( instance )
etag master LastChangedAt
early numbering
{
  create ( precheck, authorization : global );
  update ( precheck );
  delete;
  field ( readonly ) TravelId;
  field ( mandatory ) CustomerId;
  association _Booking { create; with draft; }

  internal action recalcTotalPrice;

  action acceptTravel result [1] $self;

  determination setStatusOpen on modify { create; }
  determination calcTotalPrice on modify { create; field BookingFee; }

  validation validateCustomer on save { create; field CustomerId; }

  side effects { field BookingFee affects field TotalPrice; }

  draft action Resume;
  draft action Edit with additional implementation;
  draft action Activate optimized;
  draft action Discard;
  draft determine action Prepare
  {
    validation validateCustomer;
  }

  mapping for zlc_travel_m
    {
      TravelId      = travel_id;
      AgencyId      = agency_id;
      CustomerId    = customer_id;
      BeginDate     = begin_date;
      EndDate       = end_date;
      BookingFee    = booking_fee;
      TotalPrice    = total_price;
      CurrencyCode  = currency_code;
      Description   = description;
      OverallStatus = overall_status;
      CreatedBy     = created_by;
      CreatedAt     = created_at;
      LastChangedBy = last_changed_by;
      LastChangedAt = last_changed_at;
      LastChgAt     = lastchgat;
    }

}

define behavior for ZR_LC_BOOKING_M alias Booking
implementation in class zbp_r_lc_booking_m unique
persistent table zlc_booking_m
draft table zlc_booking_d
lock dependent by _Travel
authorization dependent by _Travel
etag master LastChangedAt
early numbering
{
  update;
  delete;
  field ( readonly ) TravelId, BookingId;
  association _Travel { with draft; }
  association _BookingSupplement { create; with draft; }

  mapping for zlc_booking_m
    {
      TravelId      = travel_id;
      BookingId     = booking_id;
      BookingDate   = booking_date;
      CustomerId    = customer_id;
      AirlineID     = carrier_id;
      ConnectionId  = connection_id;
      FlightDate    = flight_date;
      FlightPrice   = flight_price;
      CurrencyCode  = currency_code;
      BookingStatus = booking_status;
      LastChangedAt = last_changed_at;
    }

}

define behavior for ZR_LC_BOOKSUPPL_M alias BookSuppl
implementation in class zbp_r_lc_booksuppl_m unique
persistent table zlc_booksuppl_m
draft table zlc_booksuppl_d
lock dependent by _Travel
authorization dependent by _Travel
etag master LastChangedAt
early numbering
{
  update;
  delete;
  field ( readonly ) TravelId, BookingId, BookingSupplementId;
  association _Travel { with draft; }
  association _Booking { with draft; }

  mapping for zlc_booksuppl_m
    {
      TravelId            = travel_id;
      BookingId           = booking_id;
      BookingSupplementId = booking_supplement_id;
      SupplementId        = supplement_id;
      Price               = price;
      CurrencyCode        = currency_code;
      LastChangedAt       = last_changed_at;
    }

}
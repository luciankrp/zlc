managed implementation in class zbp_travel_lc_m unique; // implementation in class zbp_i_travel_lc_m unique;
strict ( 2 );
with draft;

define behavior for ZI_TRAVEL_LC_M alias Travel
implementation in class zbp_travel_lc_m unique
persistent table ztravel_lc_m
draft table ztravel_lc_m_d
with additional save
lock master
total etag LastChangedAt
authorization master ( instance )
etag master LastChangedAt
early numbering
{
  create ( authorization : global );
  update ( features : instance );
  delete;
  field ( readonly ) TravelId, CreatedBy, CreatedAt, LastChangedBy, LastChangedAt;
  field ( mandatory ) AgencyId, CustomerId, BeginDate, EndDate, OverallStatus, BookingFee, CurrencyCode;
  field ( features : instance ) OverallStatus;
  association _Booking { create ( features : instance ); with draft; }

  action ( features : instance ) acceptTravel result [1] $self;
  action ( features : instance ) rejectTravel result [1] $self;

  factory action copyTravel [1];

  internal action reCalcTotPrice;
  //  internal action setOverallStatus result [1] $self;

  validation validateCustomer on save { create; field CustomerId; }
  validation validateDates on save { create; field BeginDate, EndDate; }
  validation validateStatus on save { create; field OverallStatus; }

  determination calculateTotalprice on modify { create; field BookingFee, CurrencyCode; }
  determination setOverallStatus on save { create; }

  draft action Edit;
  draft action Resume;
  draft action Discard;
  draft action Activate optimized;

  draft determine action Prepare
  { validation validateCustomer; validation validateDates; validation validateStatus; }

  side effects
  { field BookingFee affects field TotalPrice;
    field OverallStatus affects permissions ( create _Booking ); }

  mapping for ztravel_lc_m
    {
      TravelId      = travel_id;
      AgencyId      = agency_id;
      CustomerId    = customer_id;
      BeginDate     = begin_date;
      EndDate       = end_date;
      BookingFee    = booking_fee;
      TotalPrice    = total_price;
      CurrencyCode  = currency_code;
      Description   = description;
      OverallStatus = overall_status;
      CreatedBy     = created_by;
      CreatedAt     = created_at;
      LastChangedBy = last_changed_by;
      LastChangedAt = last_changed_at;
    }

}

define behavior for zi_booking_lc_m alias Booking
implementation in class zbp_booking_lc_m unique
persistent table zbooking_lc_m
draft table zbooking_lc_m_d
lock dependent by _Travel
authorization dependent by _Travel
etag master LastChangedAt
early numbering
{
  update;
  delete;
  field ( readonly ) TravelId, BookingId, LastChangedAt;
  field ( mandatory ) CarrierId, ConnectionId, FlightDate, BookingStatus;
  field ( mandatory : create, readonly : update ) BookingDate, CustomerId;

  association _Travel { with draft; }
  association _BookSuppl { create ( features : instance ); with draft; }

  determination calculateTotalprice on modify { create; field FlightPrice, CurrencyCode; }

  side effects { field FlightPrice affects field _Travel.TotalPrice; }

  mapping for zbooking_lc_m
    {
      TravelId      = travel_id;
      BookingId     = booking_id;
      BookingDate   = booking_date;
      CustomerId    = customer_id;
      CarrierId     = carrier_id;
      ConnectionId  = connection_id;
      FlightDate    = flight_date;
      FlightPrice   = flight_price;
      CurrencyCode  = currency_code;
      BookingStatus = booking_status;
      LastChangedAt = last_changed_at;
    }

}

define behavior for ZI_booksuppl_lc_m alias BookSuppl
implementation in class zbp_booksuppl_lc_m unique
//persistent table zbooksuppl_lc_m
with unmanaged save with full data
lock dependent by _Travel
draft table zbooksupl_lc_m_d
authorization dependent by _Travel
etag master LastChangedAt
early numbering
{
  update;
  delete;
  field ( readonly ) TravelId, BookingId, BookingSupplementId, LastChangedAt;
  field ( mandatory ) SupplementId, Price;
  association _Travel { with draft; }
  association _Booking { with draft; }

  determination calculateTotalprice on modify { create; field Price, CurrencyCode; }

  side effects { field Price affects field _Travel.TotalPrice; }

  mapping for zbooksuppl_lc_m
    {
      TravelId            = travel_id;
      BookingId           = booking_id;
      BookingSupplementId = booking_supplement_id;
      SupplementId        = supplement_id;
      Price               = price;
      CurrencyCode        = currency_code;
      LastChangedAt       = last_changed_at;
    }

}